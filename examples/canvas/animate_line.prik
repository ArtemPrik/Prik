import "prik.ui.Canvas"
import "prik.lang.Thread"
import "prik.util.Random"

var w = 800  var h = 600
window("Animate line", w, h)

// Крайние точки линии
var x1 = rand(w)  var y1 = rand(h)
var x2 = rand(w)  var y2 = rand(h)

// Направление движения
var d1x = rand()  var d1y = -rand()
var d2x = rand()  var d2y = rand()

// Очищаем экран
color(rand(#FFFFFF))
frect(0,0,w,h)

var hue = 0
var run = 1
while run {
    if checkhoriz(x1) d1x = -d1x
    if checkhoriz(x2) d2x = -d2x
    if checkvert(y1) d1y = -d1y
    if checkvert(y2) d2y = -d2y

    x1 = x1 + d1x  x2 = x2 + d2x
    y1 = y1 + d1y  y2 = y2 + d2y

    hue = hue + 0.0001
    if (hue >= 1) hue = 0
    sethsbcolor(hue)
    line(x1, y1, x2, y2)
    repaint()
    sleep(10)
    if keypressed() == VK_ESCAPE run = 0
}

def checkhoriz(px) return (px >= w || px < 0)
def checkvert(py)  return (py >= h || py < 0)

def floor(v) return v - v % 1

def sethsbcolor(h1) {
    var qr = h1 * 6 // временно для расчёта
    var hueindex = floor(qr) % 6
    var f = qr - floor(qr)

    if hueindex == 0  color(255, f*255, 0)
    else if hueindex == 1  color(255 - f*255, 255, 0)
    else if hueindex == 2  color(0, 255, f*255)
    else if hueindex == 3  color(0, 255-f*255, 255)
    else if hueindex == 4  color(f*255, 0, 255)
    else if hueindex == 5  color(255, 0, 255-f*255)
}
