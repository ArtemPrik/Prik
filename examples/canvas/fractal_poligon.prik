import "prik.ui.Canvas"
import "prik.lang.Math"

var msg = ""
var NUM_POINTS = 0
while (NUM_POINTS <= 2 || NUM_POINTS > 25) {
  NUM_POINTS = 0 + prompt("Сколькиугольник? (3..25)" + msg)
  if (NUM_POINTS <= 2) msg = "!! Сказано же, ну!"
  else if (NUM_POINTS > 25) msg = " Чувак, " + NUM_POINTS + " это будет ООООЧЕНЬ долго!"
}
var angle = 2*PI / NUM_POINTS;
var DIVIDER = 2.8


var w = 800  var h = 600
window("Fractal polygon demo", w, h)
fractal(w/2, h/2, w/2)
repaint()

def cpoly(cx, cy, size) {
  var ox = cx  var oy = cy - size
  var i = 0
  var ang = 0
  while i < NUM_POINTS {
    ang = ang + angle
    var nx = cx - sin(ang)*size  var ny = cy - cos(ang)*size
    line(ox, oy, nx, ny)
    ox = nx  oy = ny
    i = i + 1
  }
}

def fractal(cx, cy, size) {
  if size >= 3 {
    var s2 = size / 2
    color(0, 0, 255 - size * 255 / w/2)
    cpoly(cx, cy, size / DIVIDER)
    fractal(cx, cy - s2, size / DIVIDER)
    var n = 0
    while n < NUM_POINTS {
      fractal(cx - sin(angle*n)*s2, cy - cos(angle*n)*s2, size / DIVIDER)
      n = n + 1
    }
  }
}
